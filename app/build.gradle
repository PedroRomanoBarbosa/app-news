plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.8.10"
    id 'kotlin-kapt'
}

ext {
    ktor_version = '2.2.3'
    koin_version = '3.3.3'
    koin_android_version = '3.3.3'
    room_version = '2.5.0'
}

android {
    namespace 'com.example.newsapp'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.newsapp"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField 'String', 'NEWS_API_KEY', "\"${properties.get("NEWS_API_KEY")}\""
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    flavorDimensions 'source'
    productFlavors {
        bbc {
            applicationId 'com.example.newsapp.bbc'
            dimension 'source'
            buildConfigField 'String', 'SOURCE', '"bbc-news"'
            buildConfigField 'String', 'COUNTRY', '""'
            resValue 'string', 'app_name', "BBC News"
        }
        reuters {
            applicationId 'com.example.newsapp.reuters'
            dimension 'source'
            buildConfigField 'String', 'SOURCE', '"reuters"'
            buildConfigField 'String', 'COUNTRY', '""'
            resValue 'string', 'app_name', "Reuters"
        }
        us {
            applicationId 'com.example.newsapp.us'
            buildConfigField 'String', 'SOURCE', '"reuters"'
            buildConfigField 'String', 'COUNTRY', '"us"'
            resValue 'string', 'app_name', "USA Headlines"
        }
    }
}

dependencies {
    // Project
    implementation project(path: ':domain')

    // Android
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.paging:paging-runtime:3.1.1"

    // Image
    implementation "io.coil-kt:coil:2.2.2"

    // Koin
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_android_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Ktor client
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    // implementation "ch.qos.logback:logback-classic:1.4.5"
    // implementation "io.ktor:ktor-client-logging:$ktor_version"

    // region Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "app.cash.turbine:turbine:0.12.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Ktor Client
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
    // endregion
}